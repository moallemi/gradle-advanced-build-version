plugins {
    id 'org.jetbrains.kotlin.jvm' version '1.3.70'
    id 'java-gradle-plugin'

    id 'com.diffplug.gradle.spotless' version '3.27.2'
    id 'com.adarshr.test-logger' version '2.0.0'

    id "maven-publish"
}

apply from: 'configs/spotless.gradle'
apply from: 'configs/jacoco.gradle'

repositories {
    google()
    jcenter()
}

dependencies {
    implementation "org.jetbrains.kotlin:kotlin-stdlib:1.3.70"

    implementation 'com.android.tools.build:gradle:3.0.0'

    testImplementation gradleTestKit()
    testImplementation 'junit:junit:4.13'
    testImplementation "io.mockk:mockk:1.9.3"
}

compileKotlin {
    kotlinOptions {
        jvmTarget = "1.8"
    }
}

test {
    testLogging {
        events "passed", "skipped", "failed", "standardOut", "standardError"
    }
}

project.ext {
    artifactId = 'gradle-plugin'
    name = 'Gradle Advanced Build Version Plugin'
    pluginId = 'org.moallemi.advanced-build-version'
}

project.description = "A plugin to generate the Android version code and version name automatically."
project.group = "org.moallemi.gradle.advanced-build-version"
project.version = "1.7.0-SNAPSHOT"

gradlePlugin {
    plugins {
        myPlugin {
            id = project.pluginId
            implementationClass = 'org.moallemi.gradle.advancedbuildversion.AdvancedBuildVersionPlugin'
        }
    }
}

publishing {
    publications {
        maven(MavenPublication) {
            groupId = project.group
            artifactId = project.artifactId
            version = project.version

            from components.java
        }
    }
}

apply from: 'configs/test-setup.gradle'
